<HTML>
<HEAD>
<!-- Created by texi2html 1.57 from /home/jaffer/scheme/r4rs.txi on December, 22  2007 -->

<TITLE>Revised(4) Scheme - Example</TITLE>
</HEAD>
<BODY >
 [<A HREF="r4rs_toc.html">Contents</A>] &#160; [<A HREF="r4rs_10.html">Back</A>] &#160; [<A HREF="r4rs_10.html">Prev</A>] &#160; [<A>Up</A>] &#160; [<A HREF="r4rs_12.html">Next</A>] &#160; [<A HREF="r4rs_12.html">Forward</A>] &#160;<BR><HR><BR>


<H1><A NAME="SEC76">Example</A></H1>
<P>
 


<P>
<A NAME="IDX650"></A>


<P>
<CODE>Integrate-system</CODE> integrates the system 
<A NAME="IDX651"></A>


<center>
<P>
 y_k^^ = f_k(y_1, y_2, ..., y_n),    k = 1, ..., n
</center>


<P>
of differential equations with the method of Runge-Kutta.


<P>
The parameter <TT>system-derivative</TT> is a function that takes a system
state (a vector of values for the state variables y_1, ..., y_n)
and produces a system derivative (the values y_1^^, ...,y_n^^).  The parameter <TT>initial-state</TT> provides an initial
system state, and <TT>h</TT> is an initial guess for the length of the
integration step.


<P>
The value returned by <CODE>integrate-system</CODE> is an infinite stream of
system states.



<PRE>

(define integrate-system
  (lambda (system-derivative initial-state h)
    (let ((next (runge-kutta-4 system-derivative h)))
      (letrec ((states
                (cons initial-state
                      (delay (map-streams next
                                          states)))))
        states))))

</PRE>

<P>
<CODE>Runge-Kutta-4</CODE> takes a function, <TT>f</TT>, that produces a
<A NAME="IDX652"></A>
system derivative from a system state.  <CODE>Runge-Kutta-4</CODE>
produces a function that takes a system state and
produces a new system state.



<PRE>

(define runge-kutta-4
  (lambda (f h)
    (let ((*h (scale-vector h))
          (*2 (scale-vector 2))
          (*1/2 (scale-vector (/ 1 2)))
          (*1/6 (scale-vector (/ 1 6))))
      (lambda (y)
        ;; y is a system state
        (let* ((k0 (*h (f y)))
               (k1 (*h (f (add-vectors y (*1/2 k0)))))
               (k2 (*h (f (add-vectors y (*1/2 k1)))))
               (k3 (*h (f (add-vectors y k2)))))
          (add-vectors y
            (*1/6 (add-vectors k0
                               (*2 k1)
                               (*2 k2)
                               k3))))))))

(define elementwise
  (lambda (f)
    (lambda vectors
      (generate-vector
        (vector-length (car vectors))
        (lambda (i)
          (apply f
                 (map (lambda (v) (vector-ref  v i))
                      vectors)))))))

(define generate-vector
  (lambda (size proc)
    (let ((ans (make-vector size)))
      (letrec ((loop
                (lambda (i)
                  (cond ((= i size) ans)
                        (else
                         (vector-set! ans i (proc i))
                         (loop (+ i 1)))))))
        (loop 0)))))

(define add-vectors (elementwise +))

(define scale-vector
  (lambda (s)
    (elementwise (lambda (x) (* x s)))))

</PRE>

<P>
<CODE>Map-streams</CODE> is analogous to <CODE>map</CODE>: it applies its first
<A NAME="IDX653"></A>
<A NAME="IDX654"></A>
argument (a procedure) to all the elements of its second argument (a
stream).



<PRE>

(define map-streams
  (lambda (f s)
    (cons (f (head s))
          (delay (map-streams f (tail s))))))

</PRE>

<P>
Infinite streams are implemented as pairs whose car holds the first
element of the stream and whose cdr holds a promise to deliver the rest
of the stream.



<PRE>

(define head car)
(define tail
  (lambda (stream) (force (cdr stream))))

</PRE>

<P>
The following illustrates the use of <CODE>integrate-system</CODE> in
integrating the system


<center>
<P>
  C dv_C / dt = -i_L - v_C / R
</center>


<center>
<P>
  L di_L / dt = v_C
</center>


<P>
which models a damped oscillator.



<PRE>

(define damped-oscillator
  (lambda (R L C)
    (lambda (state)
      (let ((Vc (vector-ref state 0))
            (Il (vector-ref state 1)))
        (vector (- 0 (+ (/ Vc (* R C)) (/ Il C)))
                (/ Vc L))))))

(define the-states
  (integrate-system
     (damped-oscillator 10000 1000 .001)
     '#(1 0)
     .01))

</PRE>

<P>
         


<P></P><HR>
 [<A HREF="r4rs_toc.html">Contents</A>] &#160; [<A HREF="r4rs_10.html">Back</A>] &#160; [<A HREF="r4rs_10.html">Prev</A>] &#160; [<A>Up</A>] &#160; [<A HREF="r4rs_12.html">Next</A>] &#160; [<A HREF="r4rs_12.html">Forward</A>] &#160;

</BODY>
</HTML>
